%{
//-- don't change *any* of these: if you do, you'll break the compiler.
#include <algorithm>
#include <memory>
#include <cstring>
#include <cdk/compiler.h>
#include <cdk/types/types.h>
#include "ast/all.h"
#define LINE                         compiler->scanner()->lineno()
#define yylex()                      compiler->scanner()->scan()
#define yyerror(compiler, s)         compiler->scanner()->error(s)
//-- don't change *any* of these --- END!
%}

%parse-param {std::shared_ptr<cdk::compiler> compiler}

%union {
  //--- don't change *any* of these: if you do, you'll break the compiler.
  YYSTYPE() : type(cdk::primitive_type::create(0, cdk::TYPE_VOID)) {}
  ~YYSTYPE() {}
  YYSTYPE(const YYSTYPE &other) { *this = other; }
  YYSTYPE& operator=(const YYSTYPE &other) { type = other.type; return *this; }

  std::shared_ptr<cdk::basic_type> type;        /* expression type */
  //-- don't change *any* of these --- END!

  int                   i;	     /* integer value */
  float                 f;         /* float value */ /* TODO check */
  std::string           *s;	     /* symbol name or string literal */
  cdk::basic_node       *node;	/* node pointer */
  cdk::sequence_node    *sequence;
  cdk::expression_node  *expression; /* expression nodes */
  cdk::lvalue_node      *lvalue;

  fir::block_node       *block;    /* TODO check */
};

%token <i> tINTEGER
%token <f> tREAL    /* TODO check */
%token <s> tIDENTIFIER tSTRING
%token tWHILE tIF tWRITE tSIZEOF tLEAVE tRESTART tRETURN tREAD tBEGIN tEND

%nonassoc tIFX
%nonassoc tELSE

%right '='
%left tGE tLE tEQ tNE '>' '<'
%left '+' '-'
%left '*' '/' '%'
%nonassoc tUNARY

%type <node> stmt leave restart
%type <sequence> list
%type <expression> expr
%type <lvalue> lval
/* %type <block> block */ /* TODO check */

%{
//-- The rules below will be included in yyparse, the main parsing function.
%}
%%

list : stmt	     { $$ = new cdk::sequence_node(LINE, $1); }
     | list stmt { $$ = new cdk::sequence_node(LINE, $2, $1); }
     ;

        
stmt : expr ';'                         { $$ = new fir::evaluation_node(LINE, $1); }
     | tWRITE list ';'                  { $$ = new fir::print_node(LINE, $2); }
     | tSIZEOF '(' expr ')' ';'         { $$ = new fir::sizeof_node(LINE, $3); }
     | leave                            { $$ = $1; }
     | restart                          { $$ = $1; }
     | tRETURN ';'                      { $$ = new fir::return_node(LINE); }
     | tREAD lval ';'                   { $$ = new fir::read_node(LINE, $2); }
     | tWHILE '(' expr ')' stmt         { $$ = new fir::while_node(LINE, $3, $5); }
     | tIF '(' expr ')' stmt %prec tIFX { $$ = new fir::if_node(LINE, $3, $5); }
     | tIF '(' expr ')' stmt tELSE stmt { $$ = new fir::if_else_node(LINE, $3, $5, $7); }
     | '{' list '}'                     { $$ = $2; }
     ;

expr : tINTEGER                   { $$ = new cdk::integer_node(LINE, $1); }
     | tREAL                      { $$ = new cdk::double_node(LINE, $1); } /* TODO check */
     | tSTRING                    { $$ = new cdk::string_node(LINE, $1); }
     | '-' expr %prec tUNARY      { $$ = new cdk::neg_node(LINE, $2); }
     | expr '+' expr	         { $$ = new cdk::add_node(LINE, $1, $3); }
     | expr '-' expr	         { $$ = new cdk::sub_node(LINE, $1, $3); }
     | expr '*' expr	         { $$ = new cdk::mul_node(LINE, $1, $3); }
     | expr '/' expr	         { $$ = new cdk::div_node(LINE, $1, $3); }
     | expr '%' expr	         { $$ = new cdk::mod_node(LINE, $1, $3); }
     | expr '<' expr	         { $$ = new cdk::lt_node(LINE, $1, $3); }
     | expr '>' expr	         { $$ = new cdk::gt_node(LINE, $1, $3); }
     | expr tGE expr	         { $$ = new cdk::ge_node(LINE, $1, $3); }
     | expr tLE expr              { $$ = new cdk::le_node(LINE, $1, $3); }
     | expr tNE expr	         { $$ = new cdk::ne_node(LINE, $1, $3); }
     | expr tEQ expr	         { $$ = new cdk::eq_node(LINE, $1, $3); }
     | '(' expr ')'               { $$ = $2; }
     | lval                       { $$ = new cdk::rvalue_node(LINE, $1); }  //FIXME
     | lval '=' expr              { $$ = new cdk::assignment_node(LINE, $1, $3); }
     ;

lval : tIDENTIFIER             { $$ = new cdk::variable_node(LINE, $1); }
     ;

leave : tLEAVE ';'                      { $$ = new fir::leave_node(LINE); }
      | tLEAVE tINTEGER ';'             { $$ = new fir::leave_node(LINE, $2); }
      ;

restart : tRESTART ';'                  { $$ = new fir::restart_node(LINE); }
        | tRESTART tINTEGER ';'         { $$ = new fir::restart_node(LINE, $2); }
        ;

%%
